WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

lower = { 'a'..'z' }
alpha = { lower | 'A'..'Z' }
digit = { '0'..'9' }
number = { digit+ }
ident = { lower ~ (alpha | digit)* }

operation = _{ add | subtract | multiply | or | equals }
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
or = { "||" }
equals = { "==" }

operator_expression = { "(" ~ expression ~ operation ~ expression ~ ")" }
expression = { ident | number |  operator_expression }

if_statement = { "IF" ~ expression ~ "{" ~ statement* ~ "}" }
assign = { "ASSIGN" ~ ident ~ ":=" ~ expression ~ ";" }
return_statement = { "RETURN" ~ ident ~ ";" }

call = { "CALL" ~ ident ~ ":=" ~ ident ~ "(" ~  (expression)? ~ ("," ~ expression)* ~ ")" ~ ";" }

statement = { assign | if_statement | return_statement | call }
body = { (statement)+ }
paramters = { ident? ~ ("," ~ ident)* }
function = {"FUNCTION " ~ ident ~ "(" ~ paramters ~ ")" ~ "{" ~ body ~ "}"}

program = {
    SOI ~
    function+ ~
    EOI
}